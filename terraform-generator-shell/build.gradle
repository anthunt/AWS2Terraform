buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "edu.sc.seis.launch4j:launch4j:2.5.1"
    }
}

plugins {
    id "edu.sc.seis.launch4j" version "2.5.1"
    id "com.dorongold.task-tree" version "2.1.0"
}

apply plugin: "edu.sc.seis.launch4j"

group = 'com.anthunt.terraform'
archivesBaseName = 'terraform-generator-shell'

test {
    useJUnitPlatform()
}

bootRun {
    // https://github.com/spring-projects/spring-shell/issues/167
    standardInput = System.in
}

bootJar {
    mainClassName = "com.anthunt.terraform.generator.Aws2TerraformApplication"
    enabled = true
}

createExe.dependsOn bootJar

createExe {
    headerType      = "console"
    outfile         = "${project.name}-${project.version}.exe"
    jar             = "../libs/${project.name}-${project.version}.jar"
    libraryDir      = "libs"
    copyConfigurable= project.tasks.bootJar.outputs.files
    dontWrapJar     = false
    bundledJrePath  = "jdk-11.0.9+11-jre"
    bundledJre64Bit = true
    jdkPreference   = "jdkOnly"
    jreRuntimeBits  = "64"
    jreMinVersion   = "11"
    jreMaxVersion   = "11.0.9"
    copyright       = "github.com/anthunt"
}

tasks.register('makeJreDirectory') {
    doLast {
        mkdir layout.buildDirectory.dir("launch4j/jdk-11.0.9+11-jre")
    }
}

tasks.register('addJreToDistributable', Copy) {
    from layout.buildDirectory.dir("../../jdk-11.0.9+11-jre")
    into layout.buildDirectory.dir("launch4j/jdk-11.0.9+11-jre")
}   

addJreToDistributable.dependsOn makeJreDirectory

